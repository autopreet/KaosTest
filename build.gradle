buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id 'com.bmuschko.nexus' version '2.3.1'
}

archivesBaseName = 'kaostest'
group = "dev.manpreet"
version = "1.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.testng', name: 'testng', version: '7.4.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.12.4'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.12.3'
    testImplementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "autopreet_KaosTest"
        property "sonar.organization", "autopreet"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'KaosTest',
                'Implementation-Version': '1.0'
    }
}

task fatJar(type: Jar) {
    version("1.0-uber")
    manifest {
        attributes 'Implementation-Title': 'KaosTest',
                'Implementation-Version': '1.0'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

modifyPom {
    project {
        name 'kaostest'
        description 'Run tests randomly in parallel'
        url 'https://github.com/autopreet/kaostest'
        inceptionYear '2020'

        scm {
            url 'https://github.com/autopreet/KaosTest'
            connection 'scm:https://github.com/autopreet/KaosTest.git'
            developerConnection 'scm:https://github.com/autopreet/KaosTest.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'autopreet'
                name 'Manpreet'
                email 'manpreet.333@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
}

nexus {
    sign = true
    repositoryUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
}